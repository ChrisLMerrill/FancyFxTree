plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version 1.1
group = 'net.christophermerrill'

repositories
    {
    mavenLocal()
    jcenter()
    }

dependencies
    {
    testCompile 'junit:junit:4.12'
    testCompile 'net.christophermerrill:TestFxUtils:1.0'
    testCompile 'org.testfx:testfx-core:4.0.8-alpha'
    testCompile 'org.testfx:testfx-junit:4.0.8-alpha'
    testCompile 'org.testfx:testfx-legacy:4.0.8-alpha'
    testCompile 'org.jfxtras:openjfx-monocle:1.8.0_20'
    }

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    }

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    }

artifacts {
    archives sourcesJar
    archives javadocJar
    }

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

install {
    repositories.mavenInstaller {
    pom.project {
            packaging 'jar'
            groupId project.group
            artifactId 'FancyFxTree'
            description 'An extension of JavaFX TreeView that makes it easy to implement a sophisticated tree with editing, drag-n-drop, dynamic updates without designing your data model around the TreeView expectations.'
            version project.version
            name 'FancyFxTree' // pom.project.name must be same as bintray.pkg.name
            url 'https://github.com/ChrisLMerrill/FancyFxTree'
            inceptionYear '2017' // HARDCODED
            licenses {
                license { // HARDCODED
                    name 'Apache-2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'ChrisLMerrill'
                    name 'Chris Merrill'
                    email 'osdev@christophermerrill.net'
                }
            }
            scm {
                    connection 'https://github.com/ChrisLMerrill/FancyFxTree.git'
                    developerConnection 'https://github.com/ChrisLMerrill'
                    url 'https://github.com/ChrisLMerrill/FancyFxTree'
            }
        }
    }
}

bintray {
    user = BINTRAY_UPLOAD_USERNAME
    key = BINTRAY_UPLOAD_APIKEY
    pkg {
        repo = 'maven'
        name = 'FancyFxTree'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ChrisLMerrill/FancyFxTree.git'
        version {
            name = project.version
            released  = new Date()
            gpg {
                sign = true
                passphrase = GPG_JARSIGN_PASSPHRASE
            }
            mavenCentralSync {
                sync = true //[Default: true] Determines whether to sync the version to Maven Central.
                user = MAVENCENTRAL_USERNAME
                password = MAVENCENTRAL_PASSWORD
                close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    configurations = ['archives']
    }

}
